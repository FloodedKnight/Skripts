# ------------------------ CONFIG CONFIG CONFIG CONFIG CONFIG ----------------------


options:
	entity: wither skeleton
	name: "§x§4§4§C§D§D§B§lWITHER KING"  # made uuid of boss to ID
	
	health: 200
	attack_damage: 75
	movement_speed: 0.25
	
	helmet: player head with nbt from "{""minecraft:profile"":{properties:[{name:""textures"",value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvMmY5NTVmYTAxNjkyZjNkOWU4MGFkZWE2OTEzZTBlMzI3Nzk1ZTI4Y2NmYjhiMGE0NmEwYWY1MTk3ZDQ1MGYxMiJ9fX0=""}]}}"
	chestplate: netherite chestplate
	leggings: netherite leggings
	boots: netherite boots
	
	mainItem: netherite sword of unbreaking 100
	offHand: air
	
	leaderboard_color: "<##00EAFF>"
	leaderboard_topname: "&c&l"
	bossbar_color: light blue
	bossbar_style: solid_bar
	bossbar_radius: 20


#------------------------------------ END OF CONFIG----------------------------------
	

function server_SpawnWitherKing(p: player):
	spawn {@entity} at {_p}:
	
		add nbt from "{CustomNameVisble:1b}" to nbt of entity
	
		set max health of entity to {@health}
		set health of entity to {@health}
		set entity's attack damage attribute to {@attack_damage}
		set entity's movement speed attribute to  {@movement_speed}
		set name of entity to "%{@name}% &c%health of entity%❤"
		
		set entity's helmet to {@helmet}
		set entity's chestplate to {@chestplate}
		set entity's leggings to {@leggings}
		set entity's boots to {@boots}
		
		set entity's held item to {@mainItem}
		set the off hand item of entity to {@offHand}
		
		set {_e} to the entity
	set {_u} to uuid int array as string from {_e}
	set {mob::%{_u}%} to {_p}
	set {_bar} to bossbar with id "%{_e}'s uuid%" with title "%{@name}% &c%health of {_e}%❤" with color {@bossbar_color} with style {@bossbar_style} with progress 100
	if {Settings::%{_p}'s uuid%::Bossbars} is true:
		add {_p} to {_bar}
	loop 150 times:
		delete boss bar with id "%{_e}'s uuid%" if {_e} is not alive
		exit loop if {_e} is not alive
		loop all players in radius {@bossbar_radius} around {_e}:
			if {Settings::%loop-player's uuid%::Bossbars} is true:
				add loop-player to {_bar}
		set bossbar progress of {_bar} to ((health of {_e})/(max health of {_e}))*100
		set bar title of {_bar} to "%{@name}% &c%health of {_e}%❤"
		
		set {_ability} to random integer between 2 and 4
		
		#ABILITIES HERE
		if {_ability} is 2:
			Necromance({_e})
		if {_ability} is 3:
			SoulDrain({_e})
		if {_ability} is 4:
			DarkBubble({_e})
		
		wait 15 seconds
	kill {_e}

on damage of {@entity}:
	name of victim contains {@name}
	wait 1 tick
	set {_bar} to bossbar with id "%victim's uuid%"
	set name of victim to "%{@name}% &c%health of victim%❤"
	set bar title of {_bar} to "%{@name}% &c%health of victim%❤"
	set bossbar progress of {_bar} to ((health of victim)/(max health of victim))*100

on death of {@entity}:
	name of victim contains {@name}
	delete boss bar with id "%victim's uuid%"
	clear drops
	set {_u} to uuid int array as string from victim
	
	chance of 5.5%:
		give {mob::%{_u}%} wither skeleton skull
		
	if {mob::%{_u}%} is connected:
		give {mob::%{_u}%} 1 of ominous trial key of soul speed 1 named "§x§4§4§C§D§D§BNether Star Key" with lore "&a&lUNCOMMON KEY" with hide enchants item flag
	else if {mob::%{_u}%} is not connected:
		give {mob::%{_u}%} 1 of ominous trial key of soul speed 1 named "§x§4§4§C§D§D§BNether Star Key" with lore "&a&lUNCOMMON KEY" with hide enchants item flag
		
	add 30 to {EXP::%{mob::%{_u}%}'s uuid%::TotalCombat}
	add 30 to {EXP::%{mob::%{_u}%}'s uuid%::ResetCombat}
	if {Settings::%{mob::%{_u}%}'s uuid%::ExpDisplayAsPercent} is true:
		send action bar "&6&l+30 XP &8|| &r&b%({EXP::%{mob::%{_u}%}'s uuid%::ResetCombat})/(floor(25 * (1.3 ^ ({EXP::%{mob::%{_u}%}'s uuid%::CombatLevel} - 1)))) * 100%%% &8|| &r&6Combat" to {mob::%{_u}%}
	else:
		send action bar "&6&l+30 XP &8|| &r&b%formatNumber({EXP::%{mob::%{_u}%}'s uuid%::ResetCombat})%&7/&2%formatNumber(floor(150 * (1.3 ^ ({EXP::%{mob::%{_u}%}'s uuid%::CombatLevel} - 1))))% &8|| &r&6Combat" to {mob::%{_u}%}
		play sound "entity.experience_orb.pickup" at pitch 2 to {mob::%{_u}%}
		CombatLevelUp({mob::%{_u}%})
	set {Collections::%{mob::%{_u}%}'s uuid%::WitherKing} to ({Collections::%{mob::%{_u}%}'s uuid%::WitherKing} + 1)
	wait 1 tick
	if {Collections::%{mob::%{_u}%}'s uuid%::WitherKing} = 1:
		play sound "entity.player.levelup" to {mob::%{_u}%}
		send "" to {mob::%{_u}%}
		send " &6&lCOLLECTION UNLOCKED" to {mob::%{_u}%}
		send smallFont(" §x§4§4§C§D§D§Bwither king") to {mob::%{_u}%}
		send "" to {mob::%{_u}%}
	if {Collections::%{mob::%{_u}%}'s uuid%::WitherKing} = 5:
		CollectionLevelUp("§x§4§4§C§D§D§Bwither king", 2, {mob::%{_u}%})
	if {Collections::%{mob::%{_u}%}'s uuid%::WitherKing} = 10:
		CollectionLevelUp("§x§4§4§C§D§D§Bwither king", 3, {mob::%{_u}%})
	if {Collections::%{mob::%{_u}%}'s uuid%::WitherKing} = 25:
		CollectionLevelUp("§x§4§4§C§D§D§Bwither king", 4, {mob::%{_u}%})
	if {Collections::%{mob::%{_u}%}'s uuid%::WitherKing} = 50:
		CollectionLevelUp("§x§4§4§C§D§D§Bwither king", 5, {mob::%{_u}%})
	
	if {Collections::%{mob::%{_u}%}'s uuid%::WitherKing} = 75:
		CollectionLevelUp("§x§4§4§C§D§D§Bwither king", 6, {mob::%{_u}%})
	if {Collections::%{mob::%{_u}%}'s uuid%::WitherKing} = 100:
		CollectionLevelUp("§x§4§4§C§D§D§Bwither king", 7, {mob::%{_u}%})
	if {Collections::%{mob::%{_u}%}'s uuid%::WitherKing} = 150:
		CollectionLevelUp("§x§4§4§C§D§D§Bwither king", 8, {mob::%{_u}%})
	if {Collections::%{mob::%{_u}%}'s uuid%::WitherKing} = 200:
		CollectionLevelUp("§x§4§4§C§D§D§Bwither king", 9, {mob::%{_u}%})
	if {Collections::%{mob::%{_u}%}'s uuid%::WitherKing} = 250:
		CollectionLevelUp("§x§4§4§C§D§D§Bwither king", 10, {mob::%{_u}%})
	delete {mob::%{_u}%}
	
	
on right click:
	if name of player's held item = hexfix("&#44CDDBWither King Spawn Egg"):
		"%region at player%" contains "arena":
			if player's target block is not within 4.5 blocks of player:
				item cooldown of player's tool for player < 0.05 seconds
				set item cooldown of player's tool for player to 1 second
				remove 1 of wither spawn egg named hexfix("&#44CDDBWither King Spawn Egg") from player's inventory
				server_SpawnWitherKing(player)
			else:
				message "&8[&c✖&8] &7Please right click in the air." to player
				cancel event
		else:
			message "&8[&c✖&8] &7You must be in an arena to spawn this." to player
			cancel event

		
function SoulDrain(e: entity):
	loop all players in radius 30 of {_e}:
		send action bar "%hexfix("&#44CDDB&lWITHER KING")% &7» §x§E§F§4§7§3§ASoul Drain" to loop-player
		play sound "entity.warden.agitated" at volume 999 at pitch 0 to loop-player
		apply nausea 255 to loop-player for 8.5 seconds
		apply blindness 10 to loop-player for 6 seconds
		apply wither 6 to loop-player for 6 seconds
		apply slowness 3 to loop-player for 6 seconds
		if {Settings::%loop-player's uuid%::BossAbilitiesInChat} is true:
			send "%hexfix("&#44CDDB&lWITHER KING")% &7» §x§E§F§4§7§3§ASoul Drain" to loop-player
	loop 24 times:
		loop all players in radius 30 of {_e}:
			show 10 soul with offset of 0.5, 1.5, 0.5 and speed 0 at loop-player
			show 10 sculk charge pop with offset of 0.5, 1.5, 0.5 and speed 0 at loop-player
		wait 0.25 seconds
		
function DarkBubble(e: entity):
	loop all players in radius 30 of {_e}:
		send action bar "%hexfix("&#44CDDB&lWITHER KING")% &7» §x§E§F§4§7§3§ADark Bubble" to loop-player
		play sound "entity.illusioner.prepare_blindness" at volume 999 at pitch 1 to loop-player
		if {Settings::%loop-player's uuid%::BossAbilitiesInChat} is true:
			send "%hexfix("&#44CDDB&lWITHER KING")% &7» §x§E§F§4§7§3§ADark Bubble" to loop-player
	loop 20 times:
		loop all players in radius 10 of {_e}:
			make {_e} damage loop-player by 20
			apply wither 1 to loop-player for 1 second replacing existing effect
		rgbSphere(location at {_e}, 20, 20, 20, 10, 10, 60)
		if {_e} is not alive:
			exit loop
		wait 10 ticks
		
function Necromance(e: entity):
	loop all players in radius 30 of {_e}:
		send action bar "%hexfix("&#44CDDB&lWITHER KING")% &7» §x§E§F§4§7§3§ANecromance" to loop-player
		play sound "entity.evoker.prepare_summon" to loop-player
		if {Settings::%loop-player's uuid%::BossAbilitiesInChat} is true:
			send "%hexfix("&#44CDDB&lWITHER KING")% &7» §x§E§F§4§7§3§ANecromance" to loop-player
	
	spawn wither skeleton at location 2 in front of {_e}
	set {_w} to last spawned wither skeleton
	set name of {_w} to "§x§4§4§C§D§D§B&l%smallFont("Wither King Minion")%"
	set max health of {_w} to 30 
	set health of {_w} to 30
	set {_w}'s attack damage attribute to 10

	set {_w}'s held item to netherite sword of unbreaking 1
		
	spawn wither skeleton at location 2 behind {_e}
	set {_u} to last spawned wither skeleton
	set name of {_u} to "§x§4§4§C§D§D§B&l%smallFont("Wither King Minion")%"
	set max health of {_u} to 30 
	set health of {_u} to 30
	set {_u}'s attack damage attribute to 10
	set {_u}'s held item to netherite sword of unbreaking 1
		
	spawn wither skeleton at location 2 left of {_e}
	set {_z} to last spawned wither skeleton
	set name of {_z} to "§x§4§4§C§D§D§B&l%smallFont("Wither King Minion")%"
	set max health of {_z} to 35 
	set health of {_z} to 35
	set {_z}'s attack damage attribute to 20
	set {_z}'s held item to netherite sword of unbreaking 1
		
		
	spawn wither skeleton at location 2 right of {_e}
	set {_x} to last spawned wither skeleton
	set name of {_x} to "§x§4§4§C§D§D§B&l%smallFont("Wither King Minion")%"
	set max health of {_x} to 35 
	set health of {_x} to 35
	set {_x}'s attack damage attribute to 20
	set {_x}'s held item to netherite sword of unbreaking 1
		
	add {_x} to {_list::*}
	add {_u} to {_list::*}
	add {_z} to {_list::*}
	add {_w} to {_list::*}
	
	loop {_list::*}:
		teleport loop-value to location 5 below (location at loop-value)
		set loop-value's chestplate to iron chestplate
		set mob awareness of loop-value to false

	loop 50 times:
		teleport {_x} to location 0.1 above (location at {_x})
		teleport {_u} to location 0.1 above (location at {_u})
		teleport {_w} to location 0.1 above (location at {_w})
		teleport {_z} to location 0.1 above (location at {_z})
		draw 15 of block particle using dirt at location 1 above {_x}, {_u}, {_w}, {_z} with offset vector(0.5, 2, 0.5)
		draw 15 of block particle using green terracotta at location 1 above {_x}, {_u}, {_w}, {_z} with offset vector(0.5, 2, 0.5)
		wait 1 tick
	loop {_list::*}:
		set mob awareness of loop-value to true
	wait 75 seconds
	loop {_list::*}:
		delete entity within loop-value
		
on damage:
	name of victim is "§x§4§4§C§D§D§B&l%smallFont("Wither King Minion")%":
		damage cause is suffocation:
			cancel event
			
on death of wither skeleton:
	
	name of victim is "§x§4§4§C§D§D§B&l%smallFont("Wither King Minion")%":
		clear drops


